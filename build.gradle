group 'tikione'
version '1.3.6'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ksoichiro:gradle-build-info-plugin:0.2.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'com.github.ksoichiro.build.info'
apply plugin: 'com.github.ben-manes.versions' // https://github.com/ben-manes/gradle-versions-plugin

repositories {
    mavenCentral()
    jcenter()
}

dependencyUpdates.resolutionStrategy = {
    //noinspection GroovyAssignabilityCheck
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'Alpha', 'ALPHA', 'b', 'beta', 'Beta', 'BETA', 'rc', 'â€ŒRC', 'M', 'EA', 'pr'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

configurations {
    jar.archiveName = 'c2e.jar'
}

task copyLibs(type: Copy) {
    from configurations.runtime
    into "$buildDir/libs/lib"
}
task copyWindowsLauncher(type: Copy) {
    from "./launcher"
    into "$buildDir/libs"
    include('*.cmd')
}
task copyDoc(type: Copy) {
    from "."
    into "$buildDir/libs"
    include('*.md', "LICENSE.txt")
}
jar {
    doFirst {
        manifest {
            if (!configurations.compile.isEmpty()) {
                attributes(
                        'Main-Class': 'fr.tikione.c2e.Main',
                        'Class-Path': configurations.runtimeClasspath.files.collect { 'lib/' + it.getName() }.join(' ')
                )
            }
        }
    }
    dependsOn copyLibs
    dependsOn copyWindowsLauncher
    dependsOn copyDoc
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile group: 'net.sf.jmimemagic', name: 'jmimemagic', version: '0.1.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.11'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'org.codehaus.janino', name: 'janino', version: '3.0.7'
    compile group: 'com.github.salomonbrys.kodein', name: 'kodein', version: '4.1.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
